{
  // ref: https://github.com/JuliaLang/julia/blob/master/src/julia-parser.scm
  parserClass='org.ice1000.julia.lang.JuliaParser'
  extends='com.intellij.extapi.psi.ASTWrapperPsiElement'
  psiClassPrefix='Julia'
  psiImplClassSuffix='Impl'
  psiPackage='org.ice1000.julia.lang.psi'
  psiImplPackage='org.ice1000.julia.lang.psi.impl'

  extends('.*Op')=expr

  tokenTypeClass='org.ice1000.julia.lang.JuliaTokenType'
  elementTypeHolderClass='org.ice1000.julia.lang.psi.JuliaTypes'
  elementTypeClass='org.ice1000.julia.lang.JuliaElementType'
  psiImplUtilClass='org.ice1000.julia.lang.psi.impl.JuliaPsiImplUtils'
}

juliaFile ::= endOfLine statements

macroIdentifier ::= AT_SYM identifier
symbol ::= SYM
identifier ::= identifierImpl
private identifierChain ::= endOfLine DOT_SYM endOfLine symbol
private identifierImpl ::= (symbol identifierChain*) | (symbol? identifierChain+)

private comment ::= BLOCK_COMMENT | LINE_COMMENT
string ::= STR | RAW_STR
integer ::= INT_LITERAL
float ::= FLOAT_LITERAL | FLOAT_CONSTANT
boolean ::= TRUE_KEYWORD | FALSE_KEYWORD
char ::= CHAR_LITERAL
regex ::= REGEX_LITERAL
byteArray ::= BYTE_ARRAY_LITERAL

private endOfLineImpl ::= EOL | comment
private endOfLine ::= endOfLineImpl*
private semi ::= endOfLineImpl | SEMICOLON_SYM
statements ::= semi* (statement (semi+ statement)*)? semi*

private statement ::=
   includeStatement
 | moduleDeclaration
 | exportStatement
 | importStatement
 | usingStatement
 | tryCatchStatement
 | returnStatement
 | breakStatement
 | continueStatement
 | expr

breakStatement ::= BREAK_KEYWORD
continueStatement ::= CONTINUE_KEYWORD
includeStatement ::=
 INCLUDE_KEYWORD endOfLine
  LEFT_BRACKET endOfLine
   string endOfLine
  RIGHT_BRACKET

exportStatement ::=
 EXPORT_KEYWORD endOfLine
  (identifier | macroIdentifier)
  (endOfLine COMMA_SYM endOfLine
   (identifier | macroIdentifier))*

importStatement ::=
 IMPORT_KEYWORD endOfLine
  identifier endOfLine
  (COLON_SYM endOfLine
   identifier
   (
    endOfLine
    COMMA_SYM endOfLine
    identifier
   )*
  )?

usingStatement ::=
 USING_KEYWORD endOfLine
  identifier? (endOfLine COMMA_SYM endOfLine identifier)*

if ::=
 IF_KEYWORD statements
  elseIfClause*
  elseClause?
 END_KEYWORD

elseIfClause ::= ELSEIF_KEYWORD statements
elseClause ::= ELSE_KEYWORD statements

for ::= indexer statements END_KEYWORD
private indexer ::= FOR_KEYWORD endOfLine (multiIndexer | singleIndexer)
singleIndexer ::= symbol (IN_KEYWORD | EQ_SYM) endOfLine expr
multiIndexer ::=
 LEFT_BRACKET endOfLine
  symbol (endOfLine COMMA_SYM endOfLine symbol)+ endOfLine
 RIGHT_BRACKET
 (IN_KEYWORD | EQ_SYM) endOfLine
 expr

while ::=
 WHILE_KEYWORD endOfLine
  expr endOfLine
   statements
 END_KEYWORD

returnStatement ::= RETURN_KEYWORD expr?
tryCatchStatement ::=
 TRY_KEYWORD endOfLine
  statements
 catchClause?
 finallyClause?
 END_KEYWORD

catchClause ::=
 CATCH_KEYWORD symbol? endOfLine
 statements

finallyClause ::=
 FINALLY_KEYWORD statements

moduleType ::= BAREMODULE_KEYWORD | MODULE_KEYWORD
moduleName ::= identifierImpl
moduleDeclaration ::=
 moduleType moduleName endOfLine
  statements
 END_KEYWORD

union ::= UNION_KEYWORD tuple
tuple ::=
 LEFT_BRACKET endOfLine
  expr (endOfLine COMMA_SYM endOfLine expr)+ endOfLine COMMA_SYM? endOfLine
 RIGHT_BRACKET

private variables ::=
 LEFT_BRACKET endOfLine
  (typedVariable (endOfLine COMMA_SYM endOfLine typedVariable)*)? endOfLine
 RIGHT_BRACKET

functionName ::= SYM
function ::=
 FUNCTION_KEYWORD endOfLine
    functionName endOfLine
     (typeParameters endOfLine)?
   variables typeAnnotation? endOfLine
  statements
 END_KEYWORD

compactFunction ::=
 functionName endOfLine
  (typeParameters endOfLine)?
   variables EQ_SYM endOfLine
 statement

typeName ::= identifierImpl
abstractTypeName ::= identifierImpl
typeDeclaration ::=
 typeModifiers TYPE_KEYWORD endOfLine
  typeName endOfLine (SUBTYPE_SYM endOfLine identifier)? endOfLine
  (
   ( assignment
   | function
   | compactFunction
   | identifier
   )
   endOfLine
  )*
 END_KEYWORD

abstractTypeDeclaration ::=
 ABSTRACT_KEYWORD TYPE_KEYWORD endOfLine
  abstractTypeName endOfLine (SUBTYPE_SYM endOfLine identifier)? endOfLine
 END_KEYWORD

macro ::= 'TODO'

block ::=
 BLOCK_KEYWORD
  statements
 END_KEYWORD

assignment ::=
 typedVariable endOfLine
 (
  EQ_SYM endOfLine
  expr
 )?

typeAnnotation ::= DOUBLE_COLON endOfLine identifier
typedVariable ::=
 identifier endOfLine
  (typeAnnotation endOfLine)?
 typeParameters?

typeParameters ::=
 LEFT_B_BRACKET endOfLine
  expr (endOfLine COMMA_SYM endOfLine expr?)* endOfLine
 RIGHT_B_BRACKET

userType ::= identifier typeParameters?
typeModifiers ::= IMMUTABLE_KEYWORD?
typeAlias ::= TYPEALIAS_KEYWORD typeName typeParameters? userType

expr ::=
   primaryExpr
 | level15
 | level14
 | level13
 | level12
 | level11
 | level10
 | level9
 | level8
 | level7
 | level6
 | level5

private level5 ::= lambdaAbstraction
private lambdaAbstraction ::= symbol LAMBDA_ABSTRACTION expr {
	rightAssociative=true
}

private level6 ::=
   lessThanOrEqualOp
 | lessThanOp
 | greaterThanOp
 | greaterThanOrEqualOp
 | isntOp
 | isOp
 | equalsOp
 | unequalOp
 | elementGreaterThanOp
 | elementGreaterThanOrEqualOp
 | elementLessThanOp
 | elementLessThanOrEqualOp
 | elementEqualsOp
 | elementUnequalOp
 // | subtype

private lessThanOp ::= level7 LESS_THAN_SYM expr
private lessThanOrEqualOp ::= level7 LESS_THAN_OR_EQUAL_SYM expr
private greaterThanOp ::= level7 GREATER_THAN_SYM expr
private greaterThanOrEqualOp ::= level7 GREATER_THAN_OR_EQUAL_SYM expr
private elementEqualsOp ::= level7 ELEMENT_EQUALS_SYM expr
private elementUnequalOp ::= level7 ELEMENT_UNEQUAL_SYM expr
private elementGreaterThanOp ::= level7 ELEMENT_GREATER_THAN_SYM expr
private elementLessThanOp ::= level7 ELEMENT_LESS_THAN_SYM expr
private elementGreaterThanOrEqualOp ::= level7 ELEMENT_GREATER_THAN_OR_EQUAL_SYM expr
private elementLessThanOrEqualOp ::= level7 ELEMENT_LESS_THAN_OR_EQUAL_SYM expr
private equalsOp ::= level7 EQUALS_SYM expr
private unequalOp ::= level7 UNEQUAL_SYM expr
private isOp ::= level7 IS_SYM expr
private isntOp ::= level7 ISNT_SYM expr

private level7 ::= pipeOp
private pipeOp ::= level8 PIPE_SYM expr

private level8 ::=
   spliceOp
 | quoteOp
 | compoundQuoteOp

private spliceOp ::= level9 SLICE_SYM
// TODO review if this should be a quoted expression!!! with interpolation
private quoteOp ::= COLON_SYM expr
private compoundQuoteOp ::= QUOTE_KEYWORD expr+ END_KEYWORD

// + - ⊕ ⊖ ⊞ ⊟ .+ .- | ∪ ∨ $ ⊔
private level9 ::=
   plusOp
 | minusOp
 | bitwiseXorOp
 | elementPlusOp
 | elementMinusOp

private bitwiseXorOp ::= level10 INTERPOLATE_SYM expr
private elementMinusOp ::= level10 ELEMENT_MINUS_SYM expr
private elementPlusOp ::= level10 ELEMENT_PLUS_SYM expr
private plusOp ::= level10 PLUS_SYM expr
private minusOp ::= level10 MINUS_SYM expr

private level10 ::=
   shlOp
 | shrOp
 | ushrOp
 | elementShlOp
 | elementShrOp
 | elementUshrOp

private shrOp ::= level11 SHR_SYM expr
private ushrOp ::= level11 USHR_SYM expr
private shlOp ::= level11 SHL_SYM expr
private elementShlOp ::= level11 ELEMENT_SHL_SYM expr
private elementShrOp ::= level11 ELEMENT_SHR_SYM expr
private elementUshrOp ::= level11 ELEMENT_USHR_SYM expr

// * / % & ^
private level11 ::=
   multiplyOp
 | divideOp
 | remainderOp
 | bitwiseAndOp
 | inverseDivideOp
 | elementMultiplyOp
 | elementDivideOp
 | elementRemainderOp
 | factoriseOp

private multiplyOp ::= level12 MULTIPLY_SYM expr
private divideOp ::= level12 DIVIDE_SYM expr
private remainderOp ::= level12 REMAINDER_SYM expr
private inverseDivideOp ::= level12 INVERSE_DIV_SYM expr
private elementRemainderOp ::= level12 ELEMENT_REMAINDER_SYM expr
private elementMultiplyOp ::= level12 ELEMENT_MULTIPLY_SYM expr
private elementDivideOp ::= level12 ELEMENT_DIVIDE_SYM expr
private factoriseOp ::= level12 FACTORISE_SYM expr
private bitwiseAndOp ::= level12 BITWISE_AND_SYM expr

// // .//
private level12 ::=
   fractionOp
 | elementFractionOp

private elementFractionOp ::= level13 ELEMENT_FRACTION_SYM expr

// ^ .^
private level13 ::=
   exponentOp
 | elementExponentOp

private exponentOp ::= level14 EXPONENT_SYM expr
private fractionOp ::= level14 FRACTION_SYM expr
private elementExponentOp ::= level14 ELEMENT_EXPONENT_SYM expr

// ::
private level14 ::=
   typeOp
 | unaryTypeOp

private typeOp ::= level15 DOUBLE_COLON expr
private unaryTypeOp ::= DOUBLE_COLON expr

// TODO unfinished
private level15 ::=
   unaryPlusOp
 | unaryMinusOp
 | notOp
 | interpolateOp

private unaryMinusOp ::= MINUS_SYM expr
private notOp ::= NOT_SYM expr
private unaryPlusOp ::= PLUS_SYM expr
private interpolateOp ::= INTERPOLATE_SYM expr

private primaryExpr ::=
   string
 | char
 | integer
 | float
 | boolean
 | macroIdentifier
 | identifier
 | if
 | for
 | regex
 | byteArray
 | while
 | function
 | compactFunction
 | empty
 | union
 | macro
 | block
 | abstractTypeDeclaration
 | typeDeclaration
 | typeAlias

private inverseDivAssignOp ::= expr INVERSE_DIV_ASSIGN_SYM expr


private transposeOp ::= expr TRANSPOSE_SYM
private rangeOp ::= expr COLON_SYM expr
private remainderAssignOp ::= expr REMAINDER_ASSIGN_SYM expr
private ushrAssignOp ::= expr USHR_ASSIGN_SYM expr
private plusAssignOp ::= expr PLUS_ASSIGN_SYM expr
private minusAssignOp ::= expr MINUS_ASSIGN_SYM expr
private multiplyAssignOp ::= expr MULTIPLY_ASSIGN_SYM expr
private fractionAssignOp ::= expr FRACTION_ASSIGN_SYM expr
private divideAssignOp ::= expr DIVIDE_ASSIGN_SYM expr { rightAssociative=true }
private elementDivAssignOp ::= expr ELEMENT_DIV_ASSIGN_SYM expr { rightAssociative=true }
private elementExponentAssignOp ::= expr ELEMENT_EXPONENT_ASSIGN_SYM expr { rightAssociative=true }
private elementFractionAssignOp ::= expr ELEMENT_FRACTION_ASSIGN_SYM expr { rightAssociative=true }
private elementRemainderAssignOp ::= expr ELEMENT_REMAINDER_ASSIGN_SYM expr { rightAssociative=true }
private elementMultiplyAssignOp ::= expr ELEMENT_MULTIPLY_ASSIGN_SYM expr { rightAssociative=true }
private elementMinusAssignOp ::= expr ELEMENT_MINUS_ASSIGN_SYM expr { rightAssociative=true }
private elementPlusAssignOp ::= expr ELEMENT_PLUS_ASSIGN_SYM expr
private elementTransposeOp ::= expr ELEMENT_TRANSPOSE_SYM
private factoriseAssignOp ::= expr FACTORISE_ASSIGN_SYM expr { rightAssociative=true }
private exponentAssignOp ::= expr EXPONENT_ASSIGN_SYM expr { rightAssociative=true }
private bitWiseNotOp ::= BITWISE_NOT_SYM expr
private bitwiseAndAssignOp ::= expr BITWISE_AND_ASSIGN_SYM expr { rightAssociative=true }
private bitwiseOrOp ::= expr BITWISE_OR_SYM expr
private bitwiseOrAssignOp ::= expr BITWISE_OR_ASSIGN_SYM expr { rightAssociative=true }
private bitwiseXorAssignOp ::= expr BITWISE_XOR_ASSIGN_SYM expr { rightAssociative=true }
private arrayConstructorOp ::= expr LEFT_M_BRACKET RIGHT_M_BRACKET
private assignOp ::= expr EQ_SYM expr { rightAssociative=true }
private andOp ::= expr AND_SYM expr
private orOp ::= expr OR_SYM expr
private shlAssignOp ::= expr SHL_ASSIGN_SYM expr { rightAssociative=true }
private shrAssignOp ::= expr SHR_ASSIGN_SYM expr { rightAssociative=true }

empty ::= LEFT_M_BRACKET RIGHT_M_BRACKET
